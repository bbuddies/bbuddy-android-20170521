buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.1'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.odde.bbuddy"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "SERVER_URL", '"http://to-be-decided-later.com"'
            buildConfigField "boolean", "AUTO_LOGIN", "false"
        }
        development {
            initWith(buildTypes.debug)
            buildConfigField "String", "SERVER_URL", '"http://10.0.3.2:3000"'
            buildConfigField "boolean", "AUTO_LOGIN", "true"
        }
        debug {
            buildConfigField "String", "SERVER_URL", '"http://10.0.3.2:4000"'
            buildConfigField "boolean", "AUTO_LOGIN", "false"
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
    }
    lintOptions {
        abortOnError false
    }
}

ext {
    libVersions = [
            android    : '25.1.1',
            robobinding: '0.8.14',
            dagger     : '2.9',
            retrofit   : '2.2.0',
    ]
    testLibVersions = [
            junit       : '4.12',
            mockito     : '2.6.4',
            nestedrunner: '1.3',
            assertj     : '3.6.2',
    ]
    providedLibVersions = [
            lombok: '1.16.14',
    ]
}

dependencies {
    compile(
            [group: 'com.android.support', name: 'appcompat-v7', version: libVersions.android],
            [group: 'com.android.support', name: 'design', version: libVersions.android],
            [group: 'com.android.support', name: 'support-vector-drawable', version: libVersions.android],
            [group: 'com.google.dagger', name: 'dagger', version: libVersions.dagger],
            [group: 'com.squareup.retrofit2', name: 'retrofit', version: libVersions.retrofit],
            [group: 'com.squareup.retrofit2', name: 'converter-jackson', version: libVersions.retrofit],
    )
    compile(group: 'org.robobinding', name: 'robobinding', version: libVersions.robobinding, classifier: 'with-dependencies') {
        exclude group: 'com.google.guava', module: 'guava'
    }

    testCompile(
            [group: 'junit', name: 'junit', version: testLibVersions.junit],
            [group: 'org.mockito', name: 'mockito-core', version: testLibVersions.mockito],
            [group: 'com.nitorcreations', name: 'junit-runners', version: testLibVersions.nestedrunner],
            [group: 'org.assertj', name: 'assertj-core', version: testLibVersions.assertj],
    )

    annotationProcessor(
            [group: 'org.robobinding', name: 'codegen', version: libVersions.robobinding],
            [group: 'com.google.dagger', name: 'dagger-compiler', version: libVersions.dagger],
            [group: 'org.projectlombok', name: 'lombok', version: providedLibVersions.lombok],
    )

    provided(
            [group: 'org.projectlombok', name: 'lombok', version: providedLibVersions.lombok],
    )
}
